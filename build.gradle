apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'io.freefair.lombok'
apply plugin: 'nu.studer.jooq'

version = '0.2.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

mainClassName = 'net.snet.crm.CrmService'

project.ext {
  dropwizardVersion = '1.3.14'
  jooqVersion = '3.12.3'
  dropwizardJooqVersion = '1.3.5-1'
  lombokVersion = '1.18.10'
  jpaVersion = '2.2.0'
  serviceConfig = 'crm-service-dev.yml'
}

dependencies {
  compile "io.dropwizard:dropwizard-core:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-auth:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-client:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-jdbi:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-migrations:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-assets:$dropwizardVersion"
  compile "com.bendb.dropwizard:dropwizard-jooq:$dropwizardJooqVersion"
  compile "org.projectlombok:lombok:$lombokVersion"
  compile "org.jooq:jooq:$jooqVersion"
  compile "org.eclipse.persistence:javax.persistence:$jpaVersion"
  compile 'postgresql:postgresql:9.1-901.jdbc4'
  compile 'javax.mail:javax.mail-api:1.5.5'
  compile 'javax.mail:mail:1.4.7'
  jooqRuntime 'postgresql:postgresql:9.1-901.jdbc4'
  jooqRuntime 'ch.qos.logback:logback-classic:1.2.3'
  testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'
  testCompile 'net.bytebuddy:byte-buddy:1.10.1'
  testCompile "io.dropwizard:dropwizard-testing:$dropwizardVersion"
  testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
  testCompile 'com.h2database:h2:1.4.179'
  testCompile 'org.subethamail:subethasmtp:3.1.7'
}

lombok{
  config['lombok.anyConstructor.addConstructorProperties'] = 'true'
  config['lombok.log.fieldName'] = 'LOGGER'
}

jooq {
  version = project.ext.jooqVersion
  edition = 'OSS'
  generateSchemaSourceOnCompilation = false
  command(sourceSets.main) {
    jdbc {
      driver = 'org.postgresql.Driver'
      url = System.getenv('JOOQ_URL')
      user = System.getenv('JOOQ_USER')
      password = System.getenv('JOOQ_PASSWORD')
    }
    generator {
      name = 'org.jooq.codegen.DefaultGenerator'
      database {
        name = 'org.jooq.meta.postgres.PostgresDatabase'
        inputSchema = 'public'
        includes = 'users|customers|services|network_nodes_view'
        excludes = ''
      }
      generate {
        relations = true
        deprecated = false
        records = true
        pojos = true
        immutablePojos = true
        fluentSetters = true
        jpaAnnotations = true
        validationAnnotations = true
        daos = true
      }
      target {
        packageName = 'net.snet.crm.infra.db.command'
        directory = 'src/main/generated-java'
      }
    }
  }
  query(sourceSets.main) {
    jdbc {
      driver = 'org.postgresql.Driver'
      url = System.getenv('JOOQ_URL')
      user = System.getenv('JOOQ_USER')
      password = System.getenv('JOOQ_PASSWORD')
    }
    generator {
      name = 'org.jooq.codegen.DefaultGenerator'
      database {
        name = 'org.jooq.meta.postgres.PostgresDatabase'
        inputSchema = 'query'
        includes = 'nodes'
        excludes = ''
      }
      generate {
        relations = true
        records = false
        deprecated = false
        pojos = true
        immutablePojos = true
      }
      target {
        packageName = 'net.snet.crm.infra.db.query'
        directory = 'src/main/generated-java'
      }
    }
  }
}

repositories {
  mavenCentral()
}

run {
  args 'server', './src/dist/config/' + serviceConfig
}

buildscript {
  repositories {
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:5.1.0'
    classpath 'io.freefair.gradle:lombok-plugin:4.1.2'
    classpath 'nu.studer:gradle-jooq-plugin:4.1'
  }
}

shadowJar {
  mergeServiceFiles()
  exclude 'META-INF/*.DSA'
  exclude 'META-INF/*.RSA'
  classifier = null
  manifest {
    attributes 'Main-Class': mainClassName
  }
}

wrapper {
  gradleVersion = '5.6.2'
  distributionType = 'ALL'
}
